# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//build/test.gni")
import("//build/testing/bootfs_test.gni")
import("//src/sys/build/fuchsia_component.gni")
import("//src/sys/build/fuchsia_test_package.gni")

static_library("journal") {
  friend = [ ":*" ]

  public = [
    "background_executor.h",
    "data_streamer.h",
    "disk_struct.h",
    "format.h",
    "header_view.h",
    "initializer.h",
    "inspector_journal.h",
    "inspector_parser.h",
    "journal.h",
    "journal_writer.h",
    "operation_tracker.h",
    "replay.h",
    "superblock.h",
  ]

  sources = [
    "header_view.cc",
    "initializer.cc",
  ]

  public_deps = [
    "//src/lib/storage/vfs/cpp",
    "//src/lib/storage/vfs/cpp/transaction",
  ]

  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/third_party/ulib/cksum",
  ]

  if (is_fuchsia) {
    sources += [
      "background_executor.cc",
      "data_streamer.cc",
      "disk_struct.cc",
      "entry_view.cc",
      "entry_view.h",
      "format_assertions.h",
      "inspector_journal.cc",
      "inspector_journal_entries.cc",
      "inspector_journal_entries.h",
      "inspector_parser.cc",
      "journal.cc",
      "journal_writer.cc",
      "operation_tracker.cc",
      "replay.cc",
      "replay_tree.cc",
      "replay_tree.h",
      "superblock.cc",
    ]
    public_deps += [
      "//sdk/lib/sys/inspect/cpp",
      "//zircon/system/ulib/cobalt-client",
      "//zircon/system/ulib/disk_inspector",
      "//zircon/system/ulib/fs/metrics:metrics-cobalt",
      "//zircon/system/ulib/fs/metrics:metrics-composite-latency-event",
      "//zircon/system/ulib/fs/metrics:metrics-inspect",
      "//zircon/system/ulib/fzl",
      "//zircon/system/ulib/range",
      "//zircon/system/ulib/storage/buffer",
      "//zircon/system/ulib/storage/operation",
      "//zircon/system/ulib/trace",
    ]
    deps += [
      "//sdk/lib/fdio",
      "//sdk/lib/syslog/cpp",
      "//zircon/public/lib/fbl",
      "//zircon/public/lib/safemath",
      "//zircon/public/lib/sync",
      "//zircon/public/lib/zxc",
      "//zircon/system/ulib/disk_inspector",
      "//zircon/system/ulib/range",
      "//zircon/system/ulib/storage/buffer",
      "//zircon/system/ulib/storage/operation",
    ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":journal-fuzzers",
    ":journal-unit-tests",
  ]
}

test("journal-unit") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "background_executor_test.cc",
    "data_streamer_test.cc",
    "disk_struct_test.cc",
    "entry_view_test.cc",
    "header_view_test.cc",
    "inspector_parser_test.cc",
    "inspector_test.cc",
    "journal_test.cc",
    "operation_tracker_test.cc",
    "replay_test.cc",
    "replay_tree_test.cc",
    "superblock_test.cc",
  ]
  deps = [
    ":journal",
    "//sdk/lib/sys/inspect/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/safemath",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/block-client:fake-device",
    "//zircon/system/ulib/cobalt-client",
    "//zircon/system/ulib/cobalt-client:in-memory-logger",
    "//zircon/system/ulib/fs/metrics:metrics-cobalt",
    "//zircon/system/ulib/fs/metrics:metrics-composite-latency-event",
    "//zircon/system/ulib/fs/metrics:metrics-inspect",
    "//zircon/third_party/ulib/cksum",
  ]
}

source_set("fuzz-utils") {
  sources = [ "fuzzer_utils.cc" ]
  public_deps = [
    ":journal",
    "//sdk/lib/sys/inspect/cpp",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/cobalt-client",
    "//zircon/system/ulib/fs/metrics:metrics-cobalt",
    "//zircon/system/ulib/fs/metrics:metrics-composite-latency-event",
    "//zircon/system/ulib/fs/metrics:metrics-inspect",
    "//zircon/third_party/ulib/cksum",
  ]
}

fuchsia_component("journal-unit-tests-component") {
  component_name = "journal-unit-tests"
  testonly = true
  manifest = "meta/journal-unit-tests.cml"
  deps = [ ":journal-unit" ]
}

fuchsia_test_package("journal-unit-tests") {
  test_components = [ ":journal-unit-tests-component" ]
}

fuzzer("journal-fuzzer") {
  sources = [ "journal_fuzztest.cc" ]
  deps = [ ":fuzz-utils" ]
}

fuzzer("journal-entry-fuzzer") {
  sources = [ "entry_parse_fuzztest.cc" ]
  deps = [ ":fuzz-utils" ]
}

fuzzer("journal-replay-fuzzer") {
  sources = [ "replay_fuzztest.cc" ]
  deps = [ ":fuzz-utils" ]
}

fuzzers_package("journal-fuzzers") {
  cpp_fuzzers = [
    ":journal-fuzzer",
    ":journal-entry-fuzzer",
    ":journal-replay-fuzzer",
  ]
}
