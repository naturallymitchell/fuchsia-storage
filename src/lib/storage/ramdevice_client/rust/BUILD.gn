# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//src/lib/isolated_devmgr/isolated_devmgr.gni")
import("//src/storage/testing/driver_test_realm.gni")

group("rust") {
  public_deps = [ ":ramdevice_client" ]
}

rustc_library("ramdevice_client") {
  with_unit_tests = true
  edition = "2018"
  deps = [
    "//src/lib/fdio/rust:fdio",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
  ]
  test_deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:assert_matches",
  ]
  non_rust_deps = [ "//src/lib/storage/ramdevice_client/cpp" ]

  sources = [
    "src/lib.rs",
    "src/ramdevice_sys.rs",
  ]
}

storage_driver_test_realm_v2_component("ramdevice-client-test-component") {
  deps = [ ":ramdevice_client_test" ]
}

fuchsia_test_package("ramdevice-client-tests") {
  test_components = [ ":ramdevice-client-test-component" ]
}

# Test component that exposes a /dev filesystem containing ramdisk support as
# its fuchsia.test.IsolatedDevmgr service.
isolated_devmgr_component("ramdisk-isolated-devmgr-component") {
  component_name = "ramdisk-isolated-devmgr"
  args = [
    "--sys_device=fuchsia-boot:///#driver/platform-bus.so",
    "--svc_name=fuchsia.test.IsolatedDevmgr",
    "--wait_for=sys/platform/00:00:2d/ramctl",
  ]

  deps = [
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ftl",
    "//src/devices/block/drivers/gpt",
    "//src/devices/block/drivers/mbr",
    "//src/devices/block/drivers/ramdisk",
    "//src/devices/bus/drivers/platform",
    "//src/storage/fvm/driver",
  ]
}

fuchsia_package("ramdisk-isolated-devmgr") {
  testonly = true
  deps = [ ":ramdisk-isolated-devmgr-component" ]
}

group("tests") {
  testonly = true
  deps = [ ":ramdevice-client-tests" ]
}
